000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. P3FACTUR.
000300
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700     DECIMAL-POINT IS COMMA.
000800
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100       SELECT EXTRACT ASSIGN TO EXT
001200         FILE STATUS WS-EXTRACTFS.
001300       SELECT FACTURES ASSIGN TO FAC
001400          FILE STATUS WS-FACTURES-FS.
001500       SELECT STATE ASSIGN TO STATE
001600          ORGANIZATION     IS INDEXED
001700          ACCESS MODE      IS DYNAMIC
001800          RECORD KEY       IS CODE-STATE
001900          FILE STATUS      IS FS-STATE.
002000 DATA DIVISION.
002100 FILE SECTION.
002200 FD EXTRACT.
002300  01 ENR-EXT.
002400     05 EXT-COMPANY      PIC X(30).
002500     05 EXT-ADDRESS      PIC X(100).
002600     05 EXT-CITY         PIC X(20).
002700     05 EXT-ZIP          PIC X(5).
002800     05 EXT-STATE        PIC X(2).
002900     05 EXT-ORDER        PIC 9(4).
003000     05 EXT-DATE         PIC X(10).
003100     05 EXT-DNAME        PIC X(20).
003200     05 EXT-LNAME        PIC X(20).
003300     05 EXT-FNAME        PIC X(20).
003400     05 EXT-COMMISSION   PIC 99V99.
003500     05 EXT-PRODUCT      PIC X(3).
003600     05 EXT-DESCRIPTION  PIC X(30).
003700     05 EXT-QUANTITY     PIC 9(3).
003800     05 EXT-PRICE        PIC 9(3)V9(2).
003900     05 FILLER           PIC X(4).
004000
004100 FD FACTURES.
004200  01 ENR-FAC.
004300     05 FAC-LINE         PIC X(90).
004400     05 FILLER           PIC X(4).
004500
004600 FD STATE.
004700  01 ENR-STATE.
004900     05 CODE-STATE       PIC XX.
005000     05 FULL-NAME-STATE  PIC X(20).
005100     05 FILLER           PIC X(58).
005200
005300 WORKING-STORAGE SECTION.
005400
005500*******************************************
005600*     DECLARATION DES LIGNES
005700*******************************************
005800 01 L-BLANK.
005900     05 FILLER    PIC X    VALUE SPACE.
006000 01 L-LINE        .
006100     05 FILLER    PIC XX    VALUE '  '.
006200     05 FILLER    PIC X(70) VALUE ALL '-'.
006300     05 FILLER    PIC XX    VALUE '  '.
006400
006500 01 L-LITTLE-LINE.
006600     05 FILLER                PIC X(30) VALUE ALL SPACE.
006700     05 FILLER                PIC X(40) VALUE ALL '-'.
006800
006900 01 L-COMPANY-INFO-LINE.
007000     05 FILLER                PIC X(30) VALUE ALL SPACE.
007100     05 FILLER                PIC X    VALUE '|'.
007200     05 COMPANY-INFO-CONTENT  PIC X(40).
007300     05 FILLER                PIC X    VALUE '|'.
007400
007500 01 L-ITEM-TAB-DETAIL.
007600     05 FILLER                  PIC XXX   VALUE '  |'.
007700     05 TAB-DETAIL-PRODUCT      PIC X(4) VALUE 'P_NO'.
007800     05 FILLER                  PIC X   VALUE SPACE.
007900     05 TAB-DETAIL-DESCRIPTION  PIC X(30) VALUE 'DESCRIPTION'.
008000     05 FILLER                  PIC X   VALUE SPACE.
008100     05 TAB-DETAIL-QUANTITY     PIC X(8)  VALUE 'QUANTITY'.
008200     05 FILLER                  PIC X   VALUE SPACE.
008300     05 TAB-DETAIL-PRICE        PIC X(7) VALUE 'PRICE'.
008400     05 FILLER                  PIC XXXXX VALUE SPACE.
008500     05 TAB-DETAIL-TOTAL        PIC X(11) VALUE 'TOTAL LIGNE'.
008600     05 FILLER                  PIC XXX   VALUE '|  '.
008700 01 L-ITEM-TAB-HEADER.
008800     05 FILLER                  PIC XXX   VALUE '  |'.
008900     05 FILLER                  PIC X(4) VALUE 'P_NO'.
009000     05 FILLER                  PIC X   VALUE SPACE.
009100     05 FILLER                  PIC X(30) VALUE 'DESCRIPTION'.
009200     05 FILLER                  PIC X   VALUE SPACE.
009300     05 FILLER                  PIC X(8)  VALUE 'QUANTITY'.
009400     05 FILLER                  PIC X   VALUE SPACE.
009500     05 FILLER                  PIC X(7) VALUE 'PRICE'.
009600     05 FILLER                  PIC XXXXX VALUE SPACE.
009700     05 FILLER                  PIC X(11) VALUE 'TOTAL LIGNE'.
009800     05 FILLER                  PIC XXX   VALUE '|  '.
009900
010000 01 L-DATE.
010100     05 FILLER         PIC X(3)  VALUE ALL SPACE.
010200     05 DATELINE-LIEU  PIC X(10) VALUE 'PARIS, LE '.
010300     05 DATELINE-DATE  PIC X(30).
010400
010500 01 L-ORDER-DETAIL.
010600     05 FILLER      PIC X(3) VALUE ALL SPACE.
010700     05 ORDER-TITLE PIC X(7) VALUE 'NO '.
010800     05 ORDER-DATE  PIC X(10).
010900
011000 01 L-CONTACT.
011100     05 FILLER   PIC X(3) VALUE ALL SPACE.
011200     05 FILLER  PIC X(29) VALUE 'VOTRE CONTACT AU DEPARTEMENT '.
011300     05 CONTACT-DEPTS  PIC X(64).
011400
011500 01 L-END-DETAIL.
011600     05 FILLER   PIC X(40) VALUE ALL SPACE.
011700     05 END-TITLE PIC X(19).
011800     05 END-DETAIL  PIC X(9).
011900     05 FILLER    PIC XX VALUE ' $'.
012000
012100*****************************************
012200*    DECLARATION DES VARIABLES
012300*****************************************
012400
012500 77 WS-ORDER-SAVE  PIC 9(4).
012600 77 WS-FLAG   PIC 9 VALUE ZERO.
012700     88 FIN-FICHIER VALUE 1.
013100
013200 77 WS-EXTRACTFS   PIC 99.
013300 77 WS-FACTURES-FS PIC 99.
013400 77 FS-STATE       PIC 99.
013500
013600 77 WS-ID          PIC S9(4)V COMP-3 VALUE 1.
013700 77 WS-VAR         PIC 99 VALUE ZERO.
013800 77 ED-PRICE       PIC ZZ9,99.
013900 77 ED-QUANTITY    PIC ZZ9.
014000 77 WS-TOTAL-LINE  PIC 9(4)V99.
014100 77 ED-TOTAL-LINE  PIC ZBZZ9,99.
014200 77 WS-TOTAL-HT    PIC 9(5)V99.
014300 77 ED-TOTAL-HT    PIC ZZBZZ9,99.
014400 77 WS-TVA         PIC 9(5)V9.
014500 77 ED-TVA         PIC ZZBZZ9,9.
014600 77 WS-COMMISSION  PIC 9(5)V9.
014700 77 ED-COMMISSION  PIC ZZBZZ9,9.
014800 77 WS-TOTAL-TTC   PIC 9(5)V99.
014900 77 ED-TOTAL-TTC   PIC ZZBZZ9,99.
015000 77 WS-COMMISSION-TAX  PIC 99V99.
015100
015200***************************
015300*    SYSIN
015400 01 SYSIN.
015500    05 IN-TVA             PIC 99V9.
015600 77 ED-TVA-TAUX           PIC Z9,9.
015700 77 WS-COMMISSION-PERCENT PIC 999.
015800 77 ED-COMMISSION-TAUX    PIC Z9,9.
015900
016000 01 DATETIME.
016100     05 JOUR  PIC XX.
016200     05 MOIS  PIC XX.
016300     05 ANNEE PIC XX.
016400
016500**************************************
016600*    PROCEDURE DIVISION
016700**************************************
016800 PROCEDURE DIVISION.
016900     OPEN INPUT EXTRACT
017000     OPEN OUTPUT FACTURES
017100     PERFORM LECTURE-FICHIER
017200     ACCEPT SYSIN
017300     CALL 'SPDATE' USING BY REFERENCE DATELINE-DATE
017400
017500     PERFORM UNTIL FIN-FICHIER
017600         MOVE EXT-ORDER TO WS-ORDER-SAVE
017700         PERFORM FILE-WRITE
017800         MOVE 0 TO WS-TOTAL-HT
017900         MOVE 0 TO WS-TOTAL-TTC
018000     END-PERFORM
018100
018200
018300     CLOSE EXTRACT FACTURES
018400     STOP RUN.
018500
018600*************************************
018700 FILE-WRITE.
018800     PERFORM FILE-WRITE-COMPANY-CONTENT
018900     PERFORM FILE-WRITE-HEADER
019000     PERFORM FILE-WRITE-PRODUCT-TAB
019100     PERFORM FILE-WRITE-FOOTER
019200     .
019300
019400*************************************
019500 FILE-WRITE-COMPANY-CONTENT.
019600     DISPLAY L-BLANK
019700     DISPLAY L-LITTLE-LINE
019800     WRITE ENR-FAC FROM L-LITTLE-LINE
019900
020000*    NOM DE LA SOCIETE
020100     MOVE EXT-COMPANY TO COMPANY-INFO-CONTENT
020200     DISPLAY L-COMPANY-INFO-LINE
020300     WRITE ENR-FAC FROM L-COMPANY-INFO-LINE
020400*    ADRESSE
020500     MOVE EXT-ADDRESS TO COMPANY-INFO-CONTENT
020600     DISPLAY L-COMPANY-INFO-LINE
020700     WRITE ENR-FAC FROM L-COMPANY-INFO-LINE
020800     MOVE SPACE TO COMPANY-INFO-CONTENT
020900*    VILLE ET CODE POSTAL
021000     STRING
021100         EXT-CITY DELIMITED BY SIZE
021200         ' '      DELIMITED BY SIZE
021300         EXT-ZIP  DELIMITED BY SIZE
021400         INTO COMPANY-INFO-CONTENT
021500     END-STRING
021600     DISPLAY L-COMPANY-INFO-LINE
021700     WRITE ENR-FAC FROM L-COMPANY-INFO-LINE
021800*    ETAT
021900
022000     OPEN INPUT STATE
022100     PERFORM  RECHERCHE-ETAT
022200
022500     CLOSE STATE
022600     DISPLAY L-LITTLE-LINE
022700     WRITE ENR-FAC FROM L-LITTLE-LINE
022800     .
022900
023000*************************************
023100 FILE-WRITE-HEADER.
023200
023300     DISPLAY L-DATE
023400     WRITE ENR-FAC FROM L-DATE BEFORE ADVANCING 1 LINES
023500     DISPLAY L-BLANK
023600
023700     MOVE 'NO ' TO ORDER-TITLE
023800     MOVE EXT-ORDER TO ORDER-DATE
023900     DISPLAY L-ORDER-DETAIL
024000     WRITE ENR-FAC FROM L-ORDER-DETAIL
024100
024200     MOVE 'DATE: ' TO ORDER-TITLE
024300     MOVE EXT-DATE TO ORDER-DATE
024400     DISPLAY L-ORDER-DETAIL
024500     WRITE ENR-FAC FROM L-ORDER-DETAIL BEFORE ADVANCING 1 LINES
024600     DISPLAY L-BLANK
024700
024800     MOVE ' ' TO CONTACT-DEPTS
024900     STRING EXT-DNAME DELIMITED BY SPACE
025000            ' : '     DELIMITED BY SIZE
025100            EXT-LNAME DELIMITED BY SPACE
025200            ' '       DELIMITED BY SIZE
025300            EXT-FNAME DELIMITED BY SPACE
025400            INTO CONTACT-DEPTS
025500     END-STRING
025600     DISPLAY L-CONTACT
025700     WRITE ENR-FAC FROM L-CONTACT AFTER ADVANCING 1 LINES
025800     WRITE ENR-FAC FROM L-BLANK
025900     DISPLAY L-BLANK
026000     .
026100
026200*************************************
026300 FILE-WRITE-PRODUCT-TAB.
026400     DISPLAY L-BLANK
026500     DISPLAY L-LINE
026600     WRITE ENR-FAC FROM L-LINE AFTER ADVANCING 1 LINES
026700     WRITE ENR-FAC FROM L-ITEM-TAB-HEADER
026800     DISPLAY L-ITEM-TAB-HEADER
026900     PERFORM UNTIL EXT-ORDER NOT EQUAL TO WS-ORDER-SAVE
027000            OR FIN-FICHIER
027100         MOVE EXT-PRODUCT     TO TAB-DETAIL-PRODUCT
027200         MOVE EXT-DESCRIPTION TO TAB-DETAIL-DESCRIPTION
027300         MOVE EXT-QUANTITY    TO ED-QUANTITY
027400         MOVE ED-QUANTITY    TO TAB-DETAIL-QUANTITY
027500         MOVE EXT-PRICE       TO ED-PRICE
027600         MOVE ED-PRICE        TO TAB-DETAIL-PRICE
027700         COMPUTE WS-TOTAL-LINE = EXT-PRICE * EXT-QUANTITY
027800         MOVE WS-TOTAL-LINE   TO ED-TOTAL-LINE
027900         MOVE ED-TOTAL-LINE   TO TAB-DETAIL-TOTAL
028000
028100         ADD WS-TOTAL-LINE TO WS-TOTAL-HT
028200         DISPLAY L-ITEM-TAB-DETAIL
028300         WRITE ENR-FAC FROM L-ITEM-TAB-DETAIL
028400
028500         MOVE EXT-COMMISSION TO WS-COMMISSION-TAX
028600         PERFORM LECTURE-FICHIER
028700     END-PERFORM
028800
028900     DISPLAY L-LINE
029000     WRITE ENR-FAC FROM L-LINE
029100
029200     .
029300
029400*************************************
029500 FILE-WRITE-FOOTER.
029600     DISPLAY L-BLANK
029700*    TOTAL HORS TAXE
029800     MOVE WS-TOTAL-HT TO ED-TOTAL-HT
029900     MOVE 'TOTAL HT'  TO END-TITLE
030000     MOVE ED-TOTAL-HT TO END-DETAIL
030100     DISPLAY L-END-DETAIL
030200     WRITE ENR-FAC FROM L-END-DETAIL AFTER ADVANCING 1 LINES
030300
030400*    TVA
030500     MOVE IN-TVA TO ED-TVA-TAUX
030600     COMPUTE WS-TVA = WS-TOTAL-HT * (IN-TVA / 100)
030700     MOVE WS-TVA TO ED-TVA
030800     STRING 'MONTANT TVA (' DELIMITED BY SIZE
030900            ED-TVA-TAUX     DELIMITED BY SIZE
031000            '%)'            DELIMITED BY SIZE
031100            INTO END-TITLE
031200     END-STRING
031300     MOVE ED-TVA      TO END-DETAIL
031400     DISPLAY L-END-DETAIL
031500     WRITE ENR-FAC FROM L-END-DETAIL
031600
031700*    COMMISSION
031800     COMPUTE WS-COMMISSION = WS-TOTAL-HT *
031900             WS-COMMISSION-TAX
032000     MOVE WS-COMMISSION TO ED-COMMISSION
032100     COMPUTE WS-COMMISSION-PERCENT = WS-COMMISSION-TAX * 100
032200     MOVE WS-COMMISSION-PERCENT TO ED-COMMISSION-TAUX
032300     STRING 'COMMISSION  ('     DELIMITED BY SIZE
032400            ED-COMMISSION-TAUX  DELIMITED BY SIZE
032500            '%) '               DELIMITED BY SIZE
032600            INTO END-TITLE
032700     END-STRING
032800     MOVE ED-COMMISSION TO END-DETAIL
032900     DISPLAY L-END-DETAIL
033000     WRITE ENR-FAC FROM L-END-DETAIL
033100
033200*    TOTAL TTC
033300     COMPUTE WS-TOTAL-TTC = WS-TOTAL-HT +
033400          WS-TVA
033500     MOVE WS-TOTAL-TTC TO ED-TOTAL-TTC
033600     MOVE 'TOTAL TTC'  TO END-TITLE
033700     MOVE ED-TOTAL-TTC TO END-DETAIL
033800     DISPLAY L-END-DETAIL
033900     WRITE ENR-FAC FROM L-END-DETAIL
034000     WRITE ENR-FAC FROM L-BLANK BEFORE ADVANCING PAGE
034100     .
034200
034300*************************************
034400 LECTURE-FICHIER.
034500        READ EXTRACT AT END
034600            MOVE 1 TO WS-FLAG
034700        END-READ
034800
034900        IF (WS-EXTRACTFS NOT EQUAL 0) AND (WS-FLAG NOT EQUAL 1)
035000           DISPLAY 'ERREUR BOUCLE READ ' WS-EXTRACTFS
035100            PERFORM ABEND-PROG
035200        END-IF.
035300
035400*************************************
035500 RECHERCHE-ETAT.
035510     MOVE EXT-STATE TO CODE-STATE
035512     READ STATE
035513     EVALUATE FS-STATE
035514         WHEN ZERO
035515             MOVE FULL-NAME-STATE TO COMPANY-INFO-CONTENT
035516             DISPLAY L-COMPANY-INFO-LINE
035517             WRITE ENR-FAC FROM L-COMPANY-INFO-LINE
035518
035519         WHEN 23
035520             DISPLAY 'ERROR 404: STATE NOT FOUND'
035521             DISPLAY EXT-STATE
035522         WHEN OTHER
035523             DISPLAY 'FATAL ERROR ' FS-STATE
035524             PERFORM ABEND-PROG
035525     END-EVALUATE
036610     .
036620
036700*************************************
036800 ABEND-PROG.
036900     COMPUTE WS-VAR = 1 / WS-VAR.
