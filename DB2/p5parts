000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. P5PARTS.
000300
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700     DECIMAL-POINT IS COMMA.
000800
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100           SELECT QUANTITYTAB ASSIGN TO QTYTAB.
001800 DATA DIVISION.
001900 FILE SECTION.
002000 FD QUANTITYTAB.
002100 01 ENR-TAB.
002200     05 TAB-LINE    PIC X(177).
002300     05 FILLER      PIC X(3).
003502
003503 WORKING-STORAGE SECTION.
003504********************************************
003505*    DECLARE SQL RELATED VARIABLE
003506********************************************
003507     EXEC SQL INCLUDE SQLCA     END-EXEC.
003508     EXEC SQL INCLUDE PARTS     END-EXEC.
003509     EXEC SQL INCLUDE SUPPLIER  END-EXEC.
003510     EXEC SQL INCLUDE PARTSUPP  END-EXEC.
003515
003516     EXEC SQL
003517        DECLARE CPARTSUPP CURSOR
003518        FOR
003519        SELECT P.PNAME,S.SNAME,PS.QTY
003520         FROM API5.SUPPLIER S
003540         LEFT JOIN API5.PARTSUPP PS ON S.SNO = PS.SNO
003541         FULL JOIN API5.PARTS P ON P.PNO = PS.PNO
003542         ORDER BY S.SNO
003543     END-EXEC.
003544
003545     EXEC SQL DECLARE CPARTSNAME CURSOR
003546          FOR
003547          SELECT PNAME FROM API5.PARTS
003548     END-EXEC.
003549
003550 77 INDIC-PNAME PIC S9(4) COMP VALUE -1.
003551 77 INDIC-QTY   PIC S9(4) COMP VALUE -1.
003552
003553********************************************
003554*    DECLARE TAB LINES
003560********************************************
003570 01 L-LINE PIC X(177) VALUE ALL '-'.
003580
003590 01 L-TAB-HEADER.
003600     05 FILLER     PIC X     VALUE '|'.
003700     05 FILLER     PIC X(20) VALUE ALL SPACE.
003800     05 FILLER     PIC X     VALUE '|'.
003900     05 PART-NAME-1  PIC X(30) .
004000     05 FILLER     PIC X     VALUE '|'.
004100     05 PART-NAME-2  PIC X(30) .
004200     05 FILLER     PIC X     VALUE '|'.
004300     05 PART-NAME-3  PIC X(30) .
004400     05 FILLER     PIC X     VALUE '|'.
004500     05 PART-NAME-4  PIC X(30) .
004600     05 FILLER     PIC X     VALUE '|'.
004700     05 PART-NAME-5  PIC X(30) .
004800     05 FILLER     PIC X     VALUE '|'.
004900
005000 01 L-TAB-CONTENT.
005100     05 FILLER       PIC X     VALUE '|'.
005200     05 SUPPLIER-NAME PIC X(20).
005300     05 FILLER       PIC XXX   VALUE '|  '.
005400     05 QTY-PARTS-1  PIC X(28) VALUE '0'.
005500     05 FILLER       PIC XXX   VALUE '|  '.
005600     05 QTY-PARTS-2  PIC X(28) VALUE '0'.
005700     05 FILLER       PIC XXX   VALUE '|  '.
005800     05 QTY-PARTS-3  PIC X(28) VALUE '0'.
005900     05 FILLER       PIC XXX   VALUE '|  '.
006000     05 QTY-PARTS-4  PIC X(28) VALUE '0'.
006100     05 FILLER       PIC XXX   VALUE '|  '.
006200     05 QTY-PARTS-5  PIC X(28) VALUE '0'.
006300     05 FILLER       PIC XXX   VALUE '|  '.
006400
006500********************************************
006600*    DECLARE OTHER VARIABLES
006601********************************************
006602
006603 77 WS-ID                PIC S9(4)V COMP-3 VALUE 1.
006604 77 WS-VAR               PIC 99 VALUE ZERO.
006605 77 ED-SQLCODE           PIC 9(10).
006606 77 ED-QTY               PIC Z9.
006607 77 WS-SNAME-SAVE        PIC X(20).
006608 77 WS-SNAME-COMPARING   PIC X(20).
006611
006612********************************************
006613 PROCEDURE DIVISION.
006614     OPEN OUTPUT QUANTITYTAB
006615     EXEC SQL  OPEN CPARTSUPP  END-EXEC
006616     PERFORM TEST-SQLCODE
006617
006618     DISPLAY L-LINE
006619     WRITE ENR-TAB     FROM L-LINE
006620     PERFORM GET-NAME-INTO-TAB-HEADER
006621     WRITE ENR-TAB     FROM L-LINE
006622     PERFORM FETCH-SQL-CPARTSUPP
006623
006624     MOVE SUPP-SNAME TO WS-SNAME-SAVE
006625     MOVE SUPP-SNAME TO WS-SNAME-COMPARING
006626
006627     PERFORM UNTIL SQLCODE NOT EQUAL ZERO
006628
006629         MOVE WS-SNAME-SAVE TO SUPPLIER-NAME
006630         PERFORM UNTIL WS-SNAME-SAVE NOT EQUAL WS-SNAME-COMPARING
006631            OR SQLCODE NOT EQUAL ZERO
006632
006633*            FILL THE TAB
006634             IF INDIC-PNAME IS NEGATIVE
006635                MOVE SPACE TO PARTS-PNAME
006636             END-IF
006640             EVALUATE PARTS-PNAME
006650                  WHEN PART-NAME-1
006651                     MOVE PSUPP-QTY TO ED-QTY
006660                     MOVE ED-QTY TO QTY-PARTS-1
006670                  WHEN PART-NAME-2
006671                     MOVE PSUPP-QTY TO ED-QTY
006680                     MOVE ED-QTY TO QTY-PARTS-2
006690                  WHEN PART-NAME-3
006691                     MOVE PSUPP-QTY TO ED-QTY
006700                     MOVE ED-QTY TO QTY-PARTS-3
006800                  WHEN PART-NAME-4
006810                     MOVE PSUPP-QTY TO ED-QTY
006900                     MOVE ED-QTY TO QTY-PARTS-4
007000                  WHEN PART-NAME-5
007010                     MOVE PSUPP-QTY TO ED-QTY
007100                     MOVE ED-QTY TO QTY-PARTS-5
007110                  WHEN SPACE
007120                     CONTINUE
007200                  WHEN OTHER
007300                      DISPLAY 'ERROR'
007310                      DISPLAY PARTS-PNAME
007400              END-EVALUATE
007410
007600             PERFORM FETCH-SQL-CPARTSUPP
007610             MOVE SUPP-SNAME TO WS-SNAME-COMPARING
007700         END-PERFORM
007701
007710         DISPLAY L-TAB-CONTENT
007711         WRITE ENR-TAB     FROM L-TAB-CONTENT
007712         MOVE SUPP-SNAME TO WS-SNAME-SAVE
007713*        RESET TAB
007714         MOVE '0'   TO QTY-PARTS-1
007715         MOVE '0'   TO QTY-PARTS-2
007716         MOVE '0'   TO QTY-PARTS-3
007717         MOVE '0'   TO QTY-PARTS-4
007718         MOVE '0'   TO QTY-PARTS-5
007719
007720         WRITE ENR-TAB FROM L-LINE
007730
007800     END-PERFORM
007900
008000     EXEC SQL CLOSE CPARTSUPP END-EXEC
008001     PERFORM TEST-SQLCODE
008002     CLOSE QUANTITYTAB
008003     GOBACK.
008004
008022
008023********************************************
008024 GET-NAME-INTO-TAB-HEADER.
008025     EXEC SQL  OPEN CPARTSNAME  END-EXEC
008026
008027     MOVE SPACE TO PARTS-PNAME
008028     PERFORM FETCH-SQL-CPARTNAME
008029     MOVE PARTS-PNAME TO PART-NAME-1
008030     MOVE SPACE TO PARTS-PNAME
008031     PERFORM FETCH-SQL-CPARTNAME
008032     MOVE PARTS-PNAME TO PART-NAME-2
008033     MOVE SPACE TO PARTS-PNAME
008040     PERFORM FETCH-SQL-CPARTNAME
008050     MOVE PARTS-PNAME TO PART-NAME-3
008051     MOVE SPACE TO PARTS-PNAME
008060     PERFORM FETCH-SQL-CPARTNAME
008070     MOVE PARTS-PNAME TO PART-NAME-4
008071     MOVE SPACE TO PARTS-PNAME
008080     PERFORM FETCH-SQL-CPARTNAME
008081     MOVE PARTS-PNAME TO PART-NAME-5
008082     MOVE SPACE TO PARTS-PNAME
008083
008084     DISPLAY L-TAB-HEADER
008085     WRITE ENR-TAB     FROM L-TAB-HEADER
008086
008087     EXEC SQL CLOSE CPARTSNAME END-EXEC
008088     .
008089
008090
008091********************************************
008092 FETCH-SQL-CPARTNAME.
008093     EXEC SQL
008094         FETCH CPARTSNAME
008095         INTO :PARTS-PNAME
008096     END-EXEC
008097     PERFORM TEST-SQLCODE
008100     .
008200
008201 FETCH-SQL-CPARTSUPP.
008202     MOVE SPACE TO PARTS-PNAME
008203     MOVE SPACE TO SUPP-SNAME
008204     EXEC SQL
008205         FETCH CPARTSUPP
008206         INTO :PARTS-PNAME:INDIC-PNAME,
008207         :SUPP-SNAME, :PSUPP-QTY:INDIC-QTY
008208     END-EXEC
008209     PERFORM TEST-SQLCODE
008210     .
008211
008212
008213 TEST-SQLCODE.
008214        EVALUATE TRUE
008215           WHEN SQLCODE = ZERO
008216                CONTINUE
008217           WHEN SQLCODE = 100
008218               CONTINUE
008219           WHEN SQLCODE > ZERO
008220                DISPLAY 'WARNING : ' SQLCODE
008221           WHEN OTHER
008222                MOVE SQLCODE TO ED-SQLCODE
008223                DISPLAY 'ANOMALIE GRAVE : ' ED-SQLCODE
008224                PERFORM ABEND-PROG
008225        END-EVALUATE.
008226 ABEND-PROG.
008227     COMPUTE WS-VAR = 1 / WS-VAR.
